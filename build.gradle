plugins {
    id 'java'
    id("org.jetbrains.kotlin.jvm") version "1.7.20-RC"
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("com.glovoapp.semantic-versioning") version "1.1.10"
}

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' } // Paper API
    maven { url "https://repo.aikar.co/content/groups/aikar/" } // Aikar Command Framework
    maven { url "https://oss.sonatype.org/content/groups/public/" } //
    maven { url "https://repo.codemc.org/repository/maven-public/" } // Item-NBT Api
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT")
    compileOnly("de.tr7zw:item-nbt-api-plugin:2.10.0-SNAPSHOT")
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

group = 'net.mc48'
version = '0.1-SNAPSHOT'

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    filter {
        it.replace('${version}', version.toString())
    }
}

shadowJar{
    relocate 'co.aikar.commands', 'net.mc48.toolbox.acf'
    relocate 'co.aikar.locales', 'net.mc48.toolbox.locales'
}
